/*
 * Copyright 2019 EPAM Systems
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id "io.spring.dependency-management" version "1.0.8.RELEASE"
    id 'java'
    id 'nu.studer.jooq' version '3.0.3'
    id "org.owasp.dependencycheck" version "5.2.0"
}

apply from: 'project-properties.gradle'
def branch = releaseMode ? '5.1.0' : 'develop'
apply from: "https://raw.githubusercontent.com/reportportal/gradle-scripts/$branch/release-commons.gradle"
apply from: "https://raw.githubusercontent.com/reportportal/gradle-scripts/$branch/copy-database-scripts.gradle"
apply from: "https://raw.githubusercontent.com/reportportal/gradle-scripts/$branch/build-quality.gradle"
apply from: "https://raw.githubusercontent.com/reportportal/gradle-scripts/$branch/jacoco.gradle"
apply from: 'jooq.gradle'

repositories {
    mavenLocal()
    mavenCentral()
    releaseMode ? maven { url "http://dl.bintray.com/epam/reportportal" } : maven { url 'https://jitpack.io' }
}

dependencyManagement {
    imports {
        mavenBom(releaseMode ? 'com.epam.reportportal:commons-bom:5.0.0-RC-5' : 'com.github.reportportal:commons-bom:7affe20')
    }
}

dependencies {
    if (releaseMode) {
        compile 'com.epam.reportportal:commons'
        compile 'com.epam.reportportal:commons-rules'
        compile 'com.epam.reportportal:commons-model'
    } else {
        compile('com.github.reportportal:commons:b10eade')
        compile 'com.github.reportportal:commons-rules:0fd2060'
        compile 'com.github.reportportal:commons-model:aad6245'
    }
    compile 'org.springframework.boot:spring-boot-starter-data-jpa'

    compile 'org.hibernate:hibernate-core:5.4.4.Final'
    compile 'org.postgresql:postgresql'
    compile 'org.jooq:jooq'
    jooqRuntime 'org.postgresql:postgresql'

    compile 'io.minio:minio:6.0.10'

    compile 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
    compile 'org.hibernate.validator:hibernate-validator:6.1.0.Final'

    compile 'commons-fileupload:commons-fileupload:1.4'
    compile 'commons-validator:commons-validator:1.6'
    compile 'org.jasypt:jasypt:1.9.2'

    compile 'io.zonky.test:embedded-postgres:1.2.1'
    compile 'org.flywaydb:flyway-core:5.2.4'

    testCompile 'org.springframework.boot:spring-boot-starter-test'
    testCompile 'org.flywaydb.flyway-test-extensions:flyway-spring-test:5.2.1'
}

test {
    useJUnitPlatform()
    testLogging {
        events = ["failed"]
        exceptionFormat = "short"
    }
}

afterReleaseBuild.dependsOn bintrayUpload
checkCommitNeeded.dependsOn removeScripts
test.dependsOn copyTestDatabaseScripts
build.dependsOn jacocoTestReport
